Attribute VB_Name = "Module1"
Sub AnalyzeAll()
    Dim ws As Worksheet
    For Each ws In ThisWorkbook.Worksheets
        AnalyzeStockData ws
        FindGreatest ws
    Next ws
End Sub

Sub AnalyzeStockData(ws As Worksheet)
    Dim LastRow As Long
    Dim Ticker As String
    Dim OpenPrice As Double
    Dim ClosePrice As Double
    Dim QuarterlyChange As Double
    Dim PercentChange As Double
    Dim TotalVolume As Double
    Dim i As Long
    Dim j As Long
    Dim k As Long
    Dim StartRow As Long
    Dim EndRow As Long

    With ws
        LastRow = .Cells(.Rows.Count, 1).End(xlUp).Row

        .Cells(1, 9).Value = "Ticker"
        .Cells(1, 10).Value = "Quarterly Change"
        .Cells(1, 11).Value = "Percent Change"
        .Cells(1, 12).Value = "Total Stock Volume"

        j = 2
        i = 2

        While i <= LastRow
            Ticker = .Cells(i, 1).Value
            StartRow = i

            While i <= LastRow And .Cells(i, 1).Value = Ticker
                i = i + 1
            Wend
            EndRow = i - 1

            OpenPrice = .Cells(StartRow, 3).Value
            ClosePrice = .Cells(EndRow, 6).Value
            TotalVolume = 0

            For k = StartRow To EndRow
                TotalVolume = TotalVolume + .Cells(k, 7).Value
            Next k

            QuarterlyChange = ClosePrice - OpenPrice
            If OpenPrice <> 0 Then
                PercentChange = (QuarterlyChange / OpenPrice)
            Else
                PercentChange = 0
            End If

            .Cells(j, 9).Value = Ticker
            .Cells(j, 10).Value = QuarterlyChange
            .Cells(j, 11).Value = PercentChange
            .Cells(j, 12).Value = TotalVolume

            With .Cells(j, 10).Interior
                If PercentChange > 0 Then
                    .ColorIndex = 4
                ElseIf PercentChange < 0 Then
                    .ColorIndex = 3
                Else
                    .ColorIndex = xlNone
                End If
            End With

            j = j + 1
        Wend

        .Columns(10).NumberFormat = "0.00"
        .Columns(11).NumberFormat = "0.00%"
        .Columns("I:L").AutoFit
    End With
End Sub

Sub FindGreatest(ws As Worksheet)
    Dim LastRow As Long
    Dim GreatestIncrease As Double
    Dim GreatestDecrease As Double
    Dim GreatestVolume As Double
    Dim GreatestIncreaseTicker As String
    Dim GreatestDecreaseTicker As String
    Dim GreatestVolumeTicker As String
    Dim i As Long

    With ws
        LastRow = .Cells(.Rows.Count, 9).End(xlUp).Row

        GreatestIncrease = -1
        GreatestDecrease = 1
        GreatestVolume = 0

        For i = 2 To LastRow
            If .Cells(i, 11).Value > GreatestIncrease Then
                GreatestIncrease = .Cells(i, 11).Value
                GreatestIncreaseTicker = .Cells(i, 9).Value
            End If

            If .Cells(i, 11).Value < GreatestDecrease Then
                GreatestDecrease = .Cells(i, 11).Value
                GreatestDecreaseTicker = .Cells(i, 9).Value
            End If

            If .Cells(i, 12).Value > GreatestVolume Then
                GreatestVolume = .Cells(i, 12).Value
                GreatestVolumeTicker = .Cells(i, 9).Value
            End If
        Next i

        .Cells(1, 16).Value = "Ticker"
        .Cells(1, 17).Value = "Value"

        .Cells(2, 15).Value = "Greatest % Increase"
        .Cells(2, 16).Value = GreatestIncreaseTicker
        .Cells(2, 17).Value = GreatestIncrease
        .Cells(2, 17).NumberFormat = "0.00%"

        .Cells(3, 15).Value = "Greatest % Decrease"
        .Cells(3, 16).Value = GreatestDecreaseTicker
        .Cells(3, 17).Value = GreatestDecrease
        .Cells(3, 17).NumberFormat = "0.00%"

        .Cells(4, 15).Value = "Greatest Total Volume"
        .Cells(4, 16).Value = GreatestVolumeTicker
        .Cells(4, 17).Value = GreatestVolume
        .Cells(4, 17).NumberFormat = "0.00E+00"

        .Columns("O:Q").AutoFit
    End With
End Sub

